apply plugin: 'com.vanniktech.maven.publish'

mavenPublish {
  releaseSigningEnabled = !getGpgKey().isEmpty()
}

publishing {
  repositories {
    maven {
      name = "localMaven"
      url = "${rootProject.buildDir}/localMaven"
    }
  }
}

def getGpgKey() {
  return hasProperty('signingKey') ? signingKey : ""
}

signing {
  def signingKey = getGpgKey()
  if (!signingKey.isEmpty()) {
    useInMemoryPgpKeys(signingKey, "")
  }
}

ext.configurePom = { pom, name, description ->
  pom.name = name
  pom.description = description
  pom.url = "https://github.com/cashapp/paparazzi/"
  pom.licenses {
    license {
      name = "The Apache Software License, Version 2.0"
      url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
    }
  }
  pom.developers {
    developer {
      id = "square"
      name = "Square, Inc."
    }
  }
  pom.scm {
    url = "https://github.com/cashapp/paparazzi/"
    connection = "scm:git:git://github.com/cashapp/paparazzi.git"
    developerConnection = "scm:git:ssh://git@github.com/cashapp/paparazzi.git"
  }
}

ext.repoDir = new File("$rootProject.buildDir/prebuilts/studio/layoutlib")